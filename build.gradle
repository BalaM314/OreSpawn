buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven-publish"

def mod_file = getModFile()
version = getVersion("VERSION", mod_file)
if (System.getenv().BUILD_NUMBER) {
    version += "." + System.getenv().BUILD_NUMBER
}
version += "-1.10.2"
group = "mmd"
archivesBaseName = "orespawn"
sourceCompatibility = targetCompatibility = "1.8"

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ""
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file "secret.json"
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = "1.10.2-12.18.2.2125"
    runDir = "run"
    mappings = "snapshot_20161111"
    makeObfSourceJar = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task apiJar(type: Jar, dependsOn: classes) {
    classifier = "api"
    from sourceSets.main.allSource
    exclude("mmd/orespawn/impl/**")
    exclude("mmd/orespawn/json/**")
    exclude("mmd/orespawn/world/**")
    exclude("mmd/orespawn/*.java")
}

task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives apiJar
    archives devJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact apiJar {
                classifier "api"
            }
            artifact devJar {
                classifier "dev"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    apiKey = secret.curseforgeAPIKey
    project {
        id = "245586"
        releaseType = "alpha"
        addGameVersion("1.10.2")
        def displayVersion = getVersion("VERSION", mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += "." + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "OreSpawn $displayVersion"
        }
        addArtifact(sourcesJar) {
            displayName = "OreSpawn $displayVersion Sources"
        }
        addArtifact(apiJar) {
            displayName = "OreSpawn $displayVersion API"
        }
        addArtifact(devJar) {
            displayName = "OreSpawn $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "OreSpawn $displayVersion Javadoc"
        }
    }
}

String getModFile() {
    String path = "";
    FileTree tree = fileTree(dir: "src/main/java")
    tree.include "**/*.java"
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim();
                if (s.startsWith("@Mod")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path;
}

String getVersion(String type, String mod_file) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}
